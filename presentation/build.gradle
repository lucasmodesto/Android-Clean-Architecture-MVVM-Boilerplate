apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'jacoco-android'

android {
    compileSdkVersion versions.compileSdkVersion
    buildToolsVersion versions.buildToolsVersion

    defaultConfig {
        applicationId "br.com.lsm.androidsample"
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode versions.versionCode
        versionName versions.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {

        release {
            minifyEnabled true
            testCoverageEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "GITHUB_API_URL", "\"https://api.github.com/graphql\""
            buildConfigField "String", "GITHUB_API_TOKEN", "\"\""
        }

        debug {
            testCoverageEnabled true
            buildConfigField "String", "GITHUB_API_URL", "\"https://api.github.com/graphql\""
            buildConfigField "String", "GITHUB_API_TOKEN", "\"\""
        }
    }

    compileOptions {
        sourceCompatibility versions.javaVersion
        targetCompatibility versions.javaVersion
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            useJUnitPlatform()
        }
    }

    useLibrary 'android.test.runner'
    useLibrary 'android.test.base'
    useLibrary 'android.test.mock'

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:3.0.2'
    }
}

jacoco {
    toolVersion = "0.8.4"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.kotlin.jdk
    implementation project(':data')
    implementation project(':domain')

    implementation deps.google.material

    // AndroidX
    implementation deps.androidx.appcompat
    implementation deps.androidx.core
    implementation deps.androidx.constraintlayout
    implementation deps.androidx.recyclerview
    implementation deps.androidx.cardview
    implementation deps.androidx.lifecycle

    // Koin
    implementation deps.koin.core
    implementation deps.koin.android
    implementation deps.koin.viewmodel
    implementation deps.koin.scope
    implementation deps.koin.ext
    testImplementation deps.koin.test

    // Image Loader
    implementation deps.picasso

    // Custom view
    implementation deps.facebook.shimmer

    // Tests
    testImplementation deps.test.junit
    testImplementation deps.test.junit5
    testImplementation deps.mockk.core
    testImplementation deps.androidx.test.truth
    testImplementation deps.google.truth
    testImplementation deps.androidx.test.arch
    testImplementation deps.test.coroutines

    androidTestImplementation deps.androidx.test.core
    androidTestImplementation deps.androidx.test.junit
    androidTestImplementation deps.androidx.test.runner
    androidTestImplementation deps.androidx.test.rules
    androidTestImplementation deps.androidx.test.truth
    androidTestImplementation deps.google.truth
    androidTestImplementation deps.mockk.android
    androidTestImplementation deps.koin.test
    androidTestImplementation deps.okhttp.idlingresource

    // Espresso
    androidTestImplementation deps.androidx.espresso.core
    androidTestImplementation deps.androidx.espresso.contrib
    androidTestImplementation deps.androidx.espresso.intents
}
